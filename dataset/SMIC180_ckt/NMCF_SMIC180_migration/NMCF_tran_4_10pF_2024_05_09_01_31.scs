simulator lang=spectre
global 0
include "/home/public/PDK/smic/18/SPDK18MSE_1833_OA_CDS_V1.11_4/smic18mse_1833_oa_cds_v1.11_4/models/spectre/ms018_enhanced_v1p11_spe.lib" section=tt
include "/home/public/PDK/smic/18/SPDK18MSE_1833_OA_CDS_V1.11_4/smic18mse_1833_oa_cds_v1.11_4/models/spectre/ms018_enhanced_v1p11_spe.lib" section=bjt_tt
include "/home/public/PDK/smic/18/SPDK18MSE_1833_OA_CDS_V1.11_4/smic18mse_1833_oa_cds_v1.11_4/models/spectre/ms018_enhanced_v1p11_spe.lib" section=dio_tt
include "/home/public/PDK/smic/18/SPDK18MSE_1833_OA_CDS_V1.11_4/smic18mse_1833_oa_cds_v1.11_4/models/spectre/ms018_enhanced_v1p11_spe.lib" section=res_tt
include "/home/public/PDK/smic/18/SPDK18MSE_1833_OA_CDS_V1.11_4/smic18mse_1833_oa_cds_v1.11_4/models/spectre/ms018_enhanced_v1p11_spe.lib" section=mim_tt
include "/home/public/PDK/smic/18/SPDK18MSE_1833_OA_CDS_V1.11_4/smic18mse_1833_oa_cds_v1.11_4/models/spectre/ms018_enhanced_v1p11_spe.lib" section=var_tt

subckt NMCF VINN VINP VOUT IBIAS VDD VSS
NM9 (net041 net035 VSS VSS) n18 w=1.04e-06 l=1.9e-07 m=1 ////**Load** transistor, 2nd stage.
NM7 (IBIASP IBIAS VSS VSS) n18 w=2.2e-07 l=1.8e-07 ////bias transistor. should not be changed
NM6 (IBIAS IBIAS VSS VSS) n18 w=2.2e-07 l=1.8e-07  ////bias transistor. should not be changed
NM4 (VOUT net041 VSS VSS) n18 w=1.283e-05 l=3.4e-07 m=2 ////**Input** transistor, Load(gm3) stage.
NM0 (net27 VINN net26 VSS) n18 w=1.07e-06 l=2.7e-07 m=1 ////**Input** transistor, 1st stage. ``NM1``
NM8 (net035 net035 VSS VSS) n18 w=1.25e-06 l=6.8e-07 m=1 ////**Load** transistor, 2nd stage.
NM2 (net26 IBIAS VSS VSS) n18 w=1.29e-06 l=1.8e-07 m=1 ////current tail transistor, 1st stage
NM1 (net28 VINP net26 VSS) n18 w=1.07e-06 l=2.7e-07 m=1 ////**Input** transistor, 1st stage. ``NM0``
PM9 (VOUT net28 VDD VDD) p18 w=3.89e-06 l=1.8e-07 m=1 ////the **Input** transistor, gmf2 stage.
PM8 (net035 net28 VDD VDD) p18 w=2.79e-06 l=2e-07 m=1 ////**Input** transistor, 2nd stage.
PM7 (net041 IBIASP VDD VDD) p18 w=3.5e-06 l=2.6e-07 m=1 ////**Load** transistor, 2nd stage.
PM6 (IBIASP IBIASP VDD VDD) p18 w=2.2e-07 l=1.8e-07 ////bias transistor. should not be changed
PM2 (VOUT IBIASP VDD VDD) p18 w=3.47e-06 l=1.9e-07 m=1 ////**Load** transistor, Load(gm3) stage.
PM1 (net27 net27 VDD VDD) p18 w=1.52e-06 l=3.4e-07 m=1 ////**Load** transistor, 1st(gm1) stage. ``PM0``
PM0 (net28 net27 VDD VDD) p18 w=1.52e-06 l=3.4e-07 m=1 ////**Load** transistor, 1st(gm1) stage. ``PM1``
C1 (net041 VOUT) capacitor c=8.37e-13 m=1 ////miller cap 2
C0 (net28 VOUT) capacitor c=8.82e-13 m=1 ////miller cap 1
ends NMCF


        V0 (VSS 0) vsource dc=0 type=dc
        V1 (VDD 0) vsource dc=1.8 type=dc
        C0 (VOUT VSS) capacitor c=1e-11
        I0 (VDD IBIAS) isource dc=1e-06 type=dc
        I1 (VOUT VINP VOUT IBIAS VDD VSS) NMCF
        V2 (VINP 0) vsource type=pulse val0=1.2 val1=0.6 period=2.5e-05 rise=1p fall=1p
    